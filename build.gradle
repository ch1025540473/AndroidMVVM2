// Top-level build file where you can add configuration options common to all sub-projects/modules.

configurations.all {
    classpath.resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
}

buildscript {
    repositories {
        jcenter {
            url "http://10.69.213.71:8081/nexus/content/repositories/jcenter/"
        }
        maven {
            url 'http://10.69.213.71:8081/nexus/content/repositories/MXCentral/'
        }
        maven {
            url 'http://10.69.213.71:8081/nexus/content/repositories/snapshots/'
        }

    }
    dependencies {
        classpath 'com.android.tools.build:gradle:2.3.1'

        classpath('org.ow2.asm:asm:5.2')
        classpath('org.ow2.asm:asm-commons:5.2')
        classpath('org.ow2.asm:asm-util:5.2')

        classpath('com.tencent.tinker:tinker-patch-gradle-plugin:1.7.5')

        classpath('com.mx.architecture:gunit:0.0.2-SNAPSHOT') {
            changing = true
        }
        //classpath project(':gunit')

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }

}

ext {
    android = [
            compileSdkVersion = 25,
            targetSdkVersion = 23,
            minSdkVersion = 14,
            versionCode = 1,
            versionName = "1.0.0.65",
            buildToolsVersion = "25.0.2",
            multiDexEnabled = true,
            testInstrumentationRunner = "android.support.test.runner.AndroidJUnitRunner"
    ]

    dependencies = [
            "appcompat"                     : "com.android.support:appcompat-v7:25.1.0",
            "espresso"                      : "com.android.support.test.espresso:espresso-core:2.2.2",
            "multidex"                      : "com.android.support:multidex:1.0.1",
            "junit"                         : "junit:junit:4.12",
            "rxjava"                        : "io.reactivex.rxjava2:rxjava:2.0.6",
            "rxandroid"                     : "io.reactivex.rxjava2:rxandroid:2.0.1",
            "recyclerview"                  : "com.android.support:recyclerview-v7:23.+",
            "pulltorefresh"                 : "com.mx.architecture:pulltorefresh:1.+",
            "eventbus"                      : "org.greenrobot:eventbus:3.0.0",
            "gson"                          : "com.google.code.gson:gson:2.6.2",
            "retrofit"                      : "com.squareup.retrofit2:retrofit:2.0.2",
            "fresco"                        : "com.facebook.fresco:fresco:0.10.0",
            "imagepipeline"                 : "com.facebook.fresco:imagepipeline-okhttp3:0.10.0",
            "drawee"                        : "com.facebook.fresco:drawee:0.10.0",
            "fbcore"                        : "com.facebook.fresco:fbcore:0.10.0",
            "converter-gson"                : "com.squareup.retrofit2:converter-gson:2.0.2",
            "adapter-rxjava"                : "com.squareup.retrofit2:adapter-rxjava:2.0.2",
            "okhttp"                        : "com.squareup.okhttp3:okhttp:3.2.0",
            "logging-interceptor"           : "com.squareup.okhttp3:logging-interceptor:3.2.0",
            "logger"                        : "com.github.orhanobut:logger:1.12",
            "intensify-image"               : "me.kareluo.intensify:image:1.1.0",
            "annotations"                   : "com.android.tools:annotations:24.5.0",

            "assertj-core"                  : 'org.assertj:assertj-core:1.7.0',
            "robolectric"                   : "org.robolectric:robolectric:3.3.2",
            "junit"                         : "junit:junit:4.12",
            "mockito"                       : "org.mockito:mockito-all:1.10.19",
            "hamcrest"                      : "org.hamcrest:hamcrest-all:1.3",
            "runner"                        : "com.android.support.test:runner:0.4.1",
            "rules"                         : "com.android.support.test:rules:0.4.1",
            "powermock-module-junit4"       : "org.powermock:powermock-module-junit4:1.6.4",
            "powermock-module-junit4-rule"  : "org.powermock:powermock-module-junit4-rule:1.6.4",
            "powermock-api-mockito"         : "org.powermock:powermock-api-mockito:1.6.4",
            "powermock-classloading-xstream": "org.powermock:powermock-classloading-xstream:1.6.4"

    ]

}


task clean(type: Delete) {
    delete rootProject.buildDir
}

allprojects {
    repositories {

        jcenter {
            url "http://10.69.213.71:8081/nexus/content/repositories/jcenter/"
        }

        maven {
            url "http://10.69.213.71:8081/nexus/content/repositories/gomeMaven/"
        }

        maven {
            url "http://10.69.213.71:8081/nexus/content/repositories/MXCentral/"
        }

        maven {
            url 'http://10.69.213.71:8081/nexus/content/repositories/snapshots/'
        }
    }

    ext {
        DEFAULT_COMPILE_SDK_VERSION = 24
        DEFAULT_BUILD_TOOLS_VERSION = '25.0.2'
    }
}

class MavenOptions {
    def artifactId
    def versionName
    def snapshot = true
    def groupId = 'com.mx.architecture'
    def packaging = 'aar'

    public String getVersionName() {
        if (snapshot) {
            return versionName + '-SNAPSHOT'
        }

        return versionName
    }

    @Override
    public String toString() {
        return String.format(
                "artifactId=%s, versionName=%s, snapshot=%s, groupId=%s, packaging=%s",
                artifactId, versionName, snapshot, groupId, packaging
        )
    }
}

def provisionMaven = { project ->
    MavenOptions nexusOptions = new MavenOptions()
    project.extensions.add('maven', nexusOptions)
}

def installMaven = { project ->
    delegate = project
    resolveStrategy = Closure.DELEGATE_FIRST

    def nexusOptions = project.maven
    if (nexusOptions.artifactId == null || nexusOptions.versionName == null) {
        return;
    }

    apply plugin: 'maven'

    def url;
    if (nexusOptions.snapshot) {
        url = 'http://10.69.213.71:8081/nexus/content/repositories/GomePlusOSSnapshot/'
    } else {
        url = 'http://10.69.213.71:8081/nexus/content/repositories/GomePlusOSRelease/'
    }

    uploadArchives {
        repositories.mavenDeployer {
            //name = 'mavenCentralReleaseDeployer'
            repository(url: url) {
                authentication(userName: "admin", password: "mx123456")
            }
            pom.version = nexusOptions.versionName
            pom.artifactId = nexusOptions.artifactId
            pom.groupId = nexusOptions.groupId
            pom.name = nexusOptions.artifactId
            pom.packaging = nexusOptions.packaging
        }
    }

    def android = getExtensions().findByName('android')
    if (android != null) {
        task androidJavadocs(type: Javadoc) {
            source = android.sourceSets.main.java.srcDirs
            classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
        }

        task androidJavadocsJar(type: Jar, dependsOn: androidJavadocs) {
            classifier = 'javadoc'
            from androidJavadocs.destinationDir
        }

        task androidSourcesJar(type: Jar) {
            classifier = 'sources'
            from android.sourceSets.main.java.sourceFiles
        }

        artifacts {
            archives androidSourcesJar
            //archives androidJavadocsJar
        }
    }
}

class CoverageOptions {
    def reportClasses = 'com/mx/demo/**'
}

def provisionTest = { project ->
    project.extensions.add('coverage', new CoverageOptions())
}

def installTest = { project ->
    delegate = project
    resolveStrategy = Closure.DELEGATE_FIRST

    def android = getExtensions().findByName('android')
    if (android == null) {
        // 无视非android工程
        return
    }

    project.apply plugin: 'jacoco'

    def coverageSourceDirs = ['src/main/java'] // 源代码路径
    def classesDir = "build/intermediates/classes/debug" // class文件所在路径
    def instrumentedClassesDir = "build/intermediates/classes/jacoco"  // 插装后的class文件位置
    def executionDataFile = "build/jacoco/jacoco.exec"  // 数据文件位置

    // 需要计算覆盖率class文件模式
    def instrumentedClassesPattern = [
            'com/mx/**/*.class'
    ]

    // 不需要计算覆盖率class文件模式
    def nonInstrumentedClassesPattern = [
            '**/R.class',
            '**/R$*.class',
            '**/*$ViewInjector*.*',
            '**/BuildConfig.*',
            '**/Manifest*.*',
            '**/BR.class',
            '**/DataBinding*.*',
            '**/databinding/*.*'
    ]

    android.testOptions.unitTests.all {
        // configure the set of classes for JUnit tests
        include '**/*Test.class'
        exclude '**/espresso/**/*.class'

        // configure max heap size of the test JVM
        maxHeapSize = "2048m"
    }

    jacoco {
        toolVersion = "0.7.1.201405082137"
    }

    // jacoco离线插装任务
    task doJacocoOfflineInstrumentation(dependsOn: ['compileDebugSources', project.configurations.jacocoAnt]) {
        inputs.files compileDebugSources.outputs.files
        def outputDir = project.file(instrumentedClassesDir)
        outputs.dir outputDir
        doFirst {
            println "AAA=" + compileDebugSources.outputs.files.isEmpty()
            if (!project.file(classesDir).exists()) {
                throw RuntimeException("No class to instrument in ${classesDir}")
            }

            project.delete(outputDir)
            ant.taskdef(
                    resource: 'org/jacoco/ant/antlib.xml',
                    classpath: project.configurations.jacocoAnt.asPath,
                    uri: 'jacoco'
            )
            ant.'jacoco:instrument'(destdir: instrumentedClassesDir) {
                fileset(
                        dir: classesDir,
                        includes: instrumentedClassesPattern.join(','),
                        excludes: nonInstrumentedClassesPattern.join(',')
                )
            }

            android.testOptions.unitTests.all {
                // 设整classpath，让单元测试优先加载已插装class文件
                classpath -= project.files(classesDir)
                classpath += project.files(instrumentedClassesDir)
                classpath += project.files(classesDir)

                // 关掉class校验，防止jvm对已插装class文件报错
                jvmArgs += '-noverify'

                // 指定数据文件位置
                systemProperties.put('jacoco-agent.destfile', project.file(executionDataFile).absolutePath)

                //forkEvery 1
            }
        }
    }
    testDebugUnitTest.dependsOn doJacocoOfflineInstrumentation

    def coverage = { buildType ->
        resolveStrategy = Closure.DELEGATE_FIRST
        group = "Coverage"
        description = "Generate Jacoco coverage reports"

        // 指定哪些class文件需要计算覆盖率
        classDirectories = project.fileTree(
                dir: classesDir,
                include: project.coverage.reportClasses,
                excludes: nonInstrumentedClassesPattern
        )

        // 指定源代码位置
        sourceDirectories = project.files(coverageSourceDirs)

        // 指定单元测试中生成的数据文件位置
        executionData = project.files(executionDataFile)

        reports {
            html.enabled = true // 启动html报告
        }
    }

    task coverageDebug(type: JacocoReport, dependsOn: "testDebugUnitTest") {
        coverage.setDelegate(delegate)
        coverage("debug")
    }
}

subprojects {
    provisionMaven(project)
    provisionTest(project)

    project.afterEvaluate {
        installMaven(project)
    }
}

/**
 * 这里需要区分一下 project.afterEvaluate 和 gradle.projectsEvaluated 两个回调的区别：
 * project.afterEvaluate ：android插件读取配置后的时点，此时没有定义任何android相关task
 * gradle.projectsEvaluated ：gradle的回调，各工程及相关插件初始化结束时点
 *
 * 由于installTest()方法需要依赖test task，只能在 gradle.projectsEvaluated 回调中可以访问到
 */
gradle.projectsEvaluated {
    subprojects {
        installTest(project)
    }
}

